version = '1.0.11'

buildscript {
    repositories {
        mavenLocal()
        ["releases", "public"].each { r ->
            maven {
                url "${nexusBaseUrl}/repositories/${r}"
                credentials {
                    username nexusUserName
                    password nexusPassword
                }
            }
        }
    }

    dependencies {
        classpath "com.xebialabs.gradle.plugins:gradle-xl-plugins-plugin:${xlPluginsPluginVersion}"
        classpath 'com.moowork.gradle:gradle-node-plugin:1.1.1'
        classpath 'com.xebialabs.gradle.plugins:gradle-xl-taskgraph-plugin:0.2.0'
    }
}

import java.nio.file.Paths

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'

apply plugin: 'com.moowork.gulp'

yarn_install {
    args = ['--mutex', 'network']
}

task createBuildDir {
    doLast {
        if (!buildDir.exists()) {
            buildDir.mkdir()
        }
    }
}

jar {
    exclude 'web/web-src'
}

build.mustRunAfter clean

task gulpCopy(type: GulpTask, dependsOn: [yarn_install]) {
    args = ["copy"]
}

processResources.dependsOn(gulpCopy)

task writeNewPom {
    doLast {
        pom {
            project {
                inceptionYear '2017'
                groupId 'com.xebialabs.deployit.plugins'
                version version
            }
        }.writeTo("pom.xml")
    }

}

build.dependsOn(writeNewPom)

node {
    version = '8.4.0'
    yarnVersion = '0.27.5'
    download = true
}

idea {
    module {
        excludeDirs += file('node_modules')
        excludeDirs += file('build')
        excludeDirs += file('.idea')
        excludeDirs += file('.gradle')
    }
}
